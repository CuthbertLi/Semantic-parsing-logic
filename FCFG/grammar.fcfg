% start S
############################
# Grammar Rules
#############################

S[SEM = <?subj(?vp)>] -> NP[NUM=?n,SEM=?subj] VP[NUM=?n,SEM=?vp]

NP[NUM=?n,SEM=<?det(?nom)> ] -> Det[NUM=?n,SEM=?det]  Nom[NUM=?n,SEM=?nom]
NP[NUM=?n,SEM=?np] -> PropN[NUM=?n,SEM=?np]

NP[NUM=sg,SEM=<\Q. (- exists x. (person(x) & Q(x)))>] -> 'nobody' | 'Nobody'
NP[NUM=sg,SEM=<\Q. exists x. (person(x) & Q(x))>] -> 'somebody' | 'Somebody'

## Copular predicates
Pred[SEM=?prd] -> PredN[SEM=?prd] | PP[+PRED,SEM=?prd] | Adj[SEM=?prd]

## Predicative NPs
PredN[NUM=?n, SEM=?nom] -> Det[NUM=?n] Nom[NUM=?n, SEM=?nom]

Nom[NUM=?n,SEM=?nom] -> N[NUM=?n,SEM=?nom]
Nom[NUM=?n,SEM=<?pp(?nom)>] -> N[NUM=?n,SEM=?nom] PP[SEM=?pp]

## Transitive verbs
VP[NUM=?n,SEM=<?v(?obj)>] -> TV[NUM=?n,SEM=?v] NP[SEM=?obj]

## Copular VPs
VP[NUM=?n,SEM=<?v(?prd)>] -> AuxP[NUM=?n,SEM=?v] Pred[SEM=?prd]

## Do auxiliaries
VP[+neg,NUM=?n,SEM=<?v(?vp)>] -> AuxP[NUM=?n,SEM=?v] VP[NUM=pl,SEM=?vp]

AuxP[NUM=?n,SEM=<?neg(?aux)>] -> Aux[NUM=?n,SEM=?aux] Neg[SEM=?neg]
AuxP[NUM=?n,SEM=?aux] -> Aux[NUM=?n,SEM=?aux]

## Intransitive verbs
VP[NUM=?n,SEM=?v] -> IV[NUM=?n,SEM=?v]

## VP-level PPs
VP[NUM=?n,SEM=<app(?pp,?vp)>] -> VP[NUM=?n,SEM=?vp] PP[-PRED,SEM=?pp]

PP[LOC=?l,PRED=?prd,SEM=<app(?p,?np)>] -> P[LOC=?l,PRED=?prd,SEM=?p] NP[LOC=?l,SEM=?np]

#############################
# Lexical Rules
#############################



PropN[NUM=sg,SEM=<\P.P(Alice)>] -> 'Alice'
PropN[NUM=sg,SEM=<\P.P(Bob)>] -> 'Bob'
PropN[NUM=sg,SEM=<\P.P(Charlie)>] -> 'Charlie'
PropN[NUM=sg,SEM=<\P.P(David)>] -> 'David'
PropN[NUM=sg,SEM=<\P.P(Evan)>] -> 'Evan'
PropN[NUM=sg,SEM=<\P.P(Frank)>] -> 'Frank'
PropN[NUM=sg,SEM=<\P.P(George)>] -> 'George'
PropN[NUM=sg,SEM=<\P.P(Hank)>] -> 'Hank'

NP[NUM=sg, SEM=<\P.\x.P(x)>] -> 'who' | 'Who'

Det[NUM=sg,SEM=<\P Q.all x.(P(x) -> Q(x))>] -> 'every' | 'Every' 
Det[NUM=pl,SEM=<\P Q.all x.(P(x) -> Q(x))>] -> 'all' | 'All'
Det[SEM=<\P Q.exists x.(P(x) & Q(x))>] -> 'some' | 'Some'
Det[NUM=sg,SEM=<\P Q.exists x.(P(x) & Q(x))>] -> 'a' | 'A'
Det[NUM=sg,SEM=<\P Q.(- exists x.(P(x) & Q(x)))>] -> 'no' | 'No'
# Det[NUM=sg,SEM=<\P Q.exists x.((P(x) & Q(x)) & all y.(P(y) -> (x = y)))>] -> 'the' | 'The' 
Det[NUM=sg,SEM=<\P Q.exists x.(P(x) & Q(x))>] -> 'the' | 'The' 

N[NUM=sg,SEM=<\x.boy(x)>] -> 'boy'
N[NUM=pl,SEM=<\x.boy(x)>] -> 'boys' 
N[NUM=sg,SEM=<\x.girl(x)>] -> 'girl'
N[NUM=pl,SEM=<\x.girl(x)>] -> 'girls'
N[NUM=sg,SEM=<\x.dog(x)>] -> 'dog'
N[NUM=pl,SEM=<\x.dog(x)>] -> 'dogs'
N[NUM=sg,SEM=<\x.student(x)>] -> 'student'
N[NUM=pl,SEM=<\x.student(x)>] -> 'students'
N[NUM=sg,SEM=<\x.person(x)>] -> 'person'
N[NUM=pl,SEM=<\x.person(x)>] -> 'persons'
N[NUM=sg,SEM=<\x.garden(x)>] -> 'garden'
N[NUM=sg,SEM=<\x.kitchen(x)>] -> 'kitchen'
NP[NUM=sg, SEM=<\Q.exists x.(STU(x) & Q(x))>] -> 'STU'
NP[NUM=sg, SEM=<\Q.exists x.(Shanghai(x) & Q(x))>] -> 'Shanghai'

Adj[SEM=<\x.happy(x)>] -> 'happy'
Adj[SEM=<\x.sad(x)>] -> 'sad'
Adj[SEM=<\x.married(x)>] -> 'married'
Adj[SEM=<\x.smart(x)>] -> 'smart'
Adj[SEM=<\x.beautiful(x)>] -> 'beautiful'

TV[NUM=sg,SEM=<\X y.X(\x.chase(y,x))>,tns=pres] -> 'chases'
TV[NUM=pl,SEM=<\X y.X(\x.chase(y,x))>,tns=pres] -> 'chase'
TV[NUM=sg,SEM=<\X y.X(\x.marry(y,x))>,tns=pres] -> 'marries'
TV[NUM=pl,SEM=<\X y.X(\x.marry(y,x))>,tns=pres] -> 'marry'
TV[NUM=sg,SEM=<\X y.X(\x.know(y,x))>,tns=pres] -> 'knows'
TV[NUM=pl,SEM=<\X y.X(\x.know(y,x))>,tns=pres] -> 'know'
TV[NUM=sg,SEM=<\X y.X(\x.see(y,x))>,tns=pres] -> 'sees'
TV[NUM=pl,SEM=<\X y.X(\x.see(y,x))>,tns=pres] -> 'see'
IV[NUM=sg,SEM=<\x.bark(x)>,tns=pres] -> 'barks'
IV[NUM=pl,SEM=<\x.bark(x)>,tns=pres] -> 'bark'
IV[NUM=sg,SEM=<\x.walk(x)>,tns=pres] -> 'walks'
IV[NUM=pl,SEM=<\x.walk(x)>,tns=pres] -> 'walk'
IV[NUM=pl,SEM=<\x.dance(x)>,tns=pres] -> 'dance'
IV[NUM=sg,SEM=<\x.dance(x)>,tns=pres] -> 'dances'

Aux[NUM=sg,SEM=<\P x.P(x)>,tns=pres] -> 'is'
Aux[NUM=pl,SEM=<\P x.P(x)>,tns=pres] -> 'are'
Aux[NUM=sg,SEM=<\P x.P(x)>,tns=pres] -> 'does'
Aux[NUM=pl,SEM=<\P x.P(x)>,tns=pres] -> 'do'

P[-PRED,SEM=<\X P x.X(\y.(P(x) & in(x,y)))>] -> 'in'
P[+PRED,SEM=<\X x.X(\y.in(x,y))>] -> 'in'
P[SEM=<\X P x.X(\y.(P(x) & with(x,y)))>] -> 'with'
P[-PRED,SEM=<\X P x.X(\y.(P(x) & at(x,y)))>] -> 'at'
P[+PRED,SEM=<\X x.X(\y.at(x,y))>] -> 'at'

Neg[SEM=<\T P.T(\x.(-  P(x)))>] -> 'not'